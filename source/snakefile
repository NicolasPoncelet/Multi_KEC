import yaml
from pathlib import Path
from script.prepare_kec_input import prepare_kec_input
from script.prepare_primer3_input import prepare_primer3_input
from script.generate_input_path import get_input_path


configfile: "config/config.yaml"

with open("config/config.yaml", "r") as infile:
    config_yaml = yaml.safe_load(infile)

GENOME_DIR = Path(config_yaml["genomes_dir"]).resolve()
OUTPUT_DIR = Path(config_yaml["analysis_dir"]).resolve()
KEC_TOOL = Path("../tools/kec-linux-x64").resolve()

genera = [d.name for d in GENOME_DIR.iterdir() if d.is_dir()]

rule all:
    input:
        expand(f"{OUTPUT_DIR}/4_Primer3_output/{{genus}}/primer3.done", genus=genera)

rule prepare_all_genomes:
    input:
        genomes_dir = GENOME_DIR
    output:
        done_flags = expand(f"{OUTPUT_DIR}/1_KEC_input/{{genus}}/flag.done", genus=genera)
    run:
        prepare_kec_input(
            str(input.genomes_dir),  # Convertir en string pour compatibilitÃ©
            Path(output.done_flags[0]).parent.parent
        )

rule kec_include:
    input:
        flag = f"{OUTPUT_DIR}/1_KEC_input/{{genus}}/flag.done"
    output:
        target_files = f"{OUTPUT_DIR}/1_KEC_input/{{genus}}/target/{{genus}}.fasta"
    params:
        kec_include_settings = config_yaml["kec_include_settings"],
        master_dir = f"{OUTPUT_DIR}/1_KEC_input/{{genus}}/master",
        pool_dir = f"{OUTPUT_DIR}/1_KEC_input/{{genus}}/pool"
    shell:
        """
        {KEC_TOOL} include \
        -m {params.master_dir} \
        -p {params.pool_dir} \
        -o {output.target_files} \
        {params.kec_include_settings}
        """

rule kec_exclude:
    input:
        target = rules.kec_include.output.target_files,
    output:
        fasta_files = f"{OUTPUT_DIR}/2_KEC_output/{{genus}}.fasta"
    params:
        kec_exclude_settings = config_yaml["kec_exclude_settings"],
        non_target = f"{OUTPUT_DIR}/1_KEC_input/{{genus}}/non_target"
    resources:
        mem_mb=40000,  
        cpus=10
    shell:
        """
        {KEC_TOOL} exclude \
        -t {input.target} \
        -n {params.non_target} \
        -o {output.fasta_files} \
        {params.kec_exclude_settings}
        """

rule prepare_primer3_input:
    input:
        fasta_files = rules.kec_exclude.output.fasta_files
    output:
        primer3_flag = f"{OUTPUT_DIR}/3_Primer3_input/{{genus}}/flag.done"
    run:
        fasta_dir = Path(str(input.fasta_files)).parent

        prepare_primer3_input(
            fasta_dir,
            f"{OUTPUT_DIR}/3_Primer3_input/"
        )

rule primer3:
    input: 
        primer3_input_flag = f"{OUTPUT_DIR}/3_Primer3_input/{{genus}}/flag.done"
    output: 
        primer3_output_flag = f"{OUTPUT_DIR}/4_Primer3_output/{{genus}}/primer3.done"
    conda:
        "envs/primer3.yaml"  
    params:
        input_dir = f"{OUTPUT_DIR}/3_Primer3_input/{{genus}}",
        output_dir = f"{OUTPUT_DIR}/4_Primer3_output/{{genus}}"
    shell:
        """
        mkdir -p {params.output_dir}
        for txt_file in {params.input_dir}/*.txt; do
            primer3_core "$txt_file" > {params.output_dir}/$(basename "$txt_file" .txt).primer
        done
        touch {output.primer3_output_flag}
        """
